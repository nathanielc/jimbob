# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=expat
pkgver=2.1.0
pkgrel=3
pkgdesc="An XML parser library"
arch=(arm)
url="http://expat.sourceforge.net/"
license=('custom')
depends=('glibc')
options=(!makeflags !strip)
source=(http://downloads.sourceforge.net/sourceforge/expat/${pkgname}-${pkgver}.tar.gz)
md5sums=('dd7dab7a5fea97d2a6a43f511449b7cd')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  source $_cross_env
  ./configure \
        --prefix=/usr \
        --build=$CHOST \
        --host=$_target
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # Can't easily check cross compiled binaries
  #make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  source $_cross_env
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING

  $STRIP $pkgdir/usr/bin/*
  $STRIP $pkgdir/usr/lib/*.a
  $STRIP $pkgdir/usr/lib/*.so*
}
