
# Maintainer: Nathaniel Cook <nvcook42@gmail.com>
pkgname='gcc-ct'
_pkgname=gcc
pkgver=4.9.0
pkgrel=1
pkgdesc="GCC cross compiler for ARM EABI and for a newlib"
arch=(arm)
url="https://gcc.gnu.org/"
license=('GPL')
makedepends=('binutils-ct')
options=(!strip)

_mpfr_ver=3.1.2
_gmp_ver=6.0.0
_mpc_ver=1.0.2

source=("ftp://gcc.gnu.org/pub/gcc/releases/$_pkgname-$pkgver/$_pkgname-$pkgver.tar.bz2"
        "http://gforge.inria.fr/frs/download.php/32210/mpfr-$_mpfr_ver.tar.bz2"
        "http://ftp.gnu.org/gnu/gmp/gmp-${_gmp_ver}a.tar.bz2"
        "http://www.multiprecision.org/mpc/download/mpc-$_mpc_ver.tar.gz"
        )

md5sums=('9709b49ae0e904cbb0a6a1b62853b556'
         'ee2c3ac63bf0c2359bf08fc3ee094c19'
         'b7ff2d88cae7f8085bd5006096eed470'
         '68fadff3358fb3e7976c7a398a0af4c3')


prepare() {
    cd "$srcdir/$_pkgname-$pkgver"

    #Extract deps
    tar xf ../mpfr-$_mpfr_ver.tar.bz2
    rm -rf mpfr
    mv -v mpfr-$_mpfr_ver mpfr

    tar xf ../gmp-${_gmp_ver}a.tar.bz2
    rm -rf gmp
    mv -v gmp-$_gmp_ver gmp

    tar xf ../mpc-$_mpc_ver.tar.gz
    rm -rf mpc
    mv -v mpc-$_mpc_ver mpc

    sed -i '/k prot/agcc_cv_libc_provides_ssp=yes' gcc/configure
}

build() {
    _builddir="$srcdir/$_pkgname-build"
    rm -rf $_builddir
    mkdir $_builddir
    cd $_builddir
    $srcdir/$_pkgname-$pkgver/configure \
        --prefix=$_ct_prefix \
        --build=$CHOST \
        --host=$CHOST \
        --target=$_target \
        --with-local-prefix=$_ct_prefix \
        --with-sysroot=$_ct_sysroot \
        --with-build-time-tools=$_ct_utils_path \
        --with-mpfr-include=$srcdir/$_pkgname-$pkgver/mpfr/src \
        --with-mpfr-lib=$_builddir/mpfr/src/.libs \
        --with-arch=$_arm_arch \
        --with-float=$_float \
        --with-fpu=$_fpu \
        --enable-clocale=gnu \
        --enable-languages=c,c++ \
        --enable-shared \
        --enable-threads=posix \
        --enable-__cxa_atexit \
        --disable-libstdcxx-pch \
        --disable-multilib \
        --disable-bootstrap \
        --disable-libgomp

    make
}

check() {
    cd "$srcdir/$_pkgname-$pkgver"
    #make -k check
}

package() {
    _builddir="$srcdir/$_pkgname-build"
    cd $_builddir
    PATH=$_ct_path:$PATH
    make DESTDIR="$pkgdir/" -j1 install

    #Remove conflicting files
    rm  $pkgdir/$_ct_prefix/share/info/dir
}
