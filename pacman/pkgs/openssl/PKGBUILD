# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=openssl
_ver=1.0.1h
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
#pkgver=$_ver
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=(arm)
url='https://www.openssl.org'
license=('custom:BSD')
depends=('zlib')
optdepends=('ca-certificates')
options=(!makeflags !strip)
backup=('etc/ssl/openssl.cnf')
source=("https://www.openssl.org/source/${pkgname}-${_ver}.tar.gz"
        "https://www.openssl.org/source/${pkgname}-${_ver}.tar.gz.asc"
        'no-rpath.patch'
        'ca-dir.patch')
md5sums=('8d6d684a9430d5cc98a62a5d8fbda8cf'
         'SKIP'
         'dc78d3d06baffc16217519242ce92478'
         '3bf51be3a1bbd262be46dc619f92aa90')

prepare() {
    cd $srcdir/$pkgname-$_ver

    # remove rpath: http://bugs.archlinux.org/task/14367
    patch -p0 -i $srcdir/no-rpath.patch
    # set ca dir to /etc/ssl by default
    patch -p0 -i $srcdir/ca-dir.patch
}

build() {
    cd $srcdir/$pkgname-$_ver
    source $_cross_env

    openssltarget='linux-armv4'

    # mark stack as non-executable: http://bugs.archlinux.org/task/12434
    ./Configure \
            --prefix=/usr \
            --openssldir=/etc/ssl \
            --libdir=lib \
            shared zlib \
            "${openssltarget}" \
            "-Wa,--noexecstack ${CPPFLAGS} ${CFLAGS} ${LDFLAGS}"

    make depend
    make
}

check() {
    cd $srcdir/$pkgname-$_ver
    # the test fails due to missing write permissions in /etc/ssl
    # revert this patch for make test
    #patch -p0 -R -i $srcdir/ca-dir.patch
    #make test
    #patch -p0 -i $srcdir/ca-dir.patch
}

package() {
    cd $srcdir/$pkgname-$_ver
    source $_cross_env
    make INSTALL_PREFIX=$pkgdir MANDIR=/usr/share/man MANSUFFIX=ssl install
    install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

    $STRIP $pkgdir/usr/bin/openssl
    $STRIP $pkgdir/usr/lib/*.a
    $STRIP $pkgdir/usr/lib/*.so*
}
