# $Id$
# Contributor: Low Kian Seong <fastmail_low@speedymail.org>
# Maintainer: dorphell <dorphell@archlinux.org>
pkgname=lzo2
pkgver=2.06
pkgrel=3
pkgdesc="Portable lossless data compression library"
arch=(arm)
url="http://www.oberhumer.com/opensource/lzo"
license=('GPL')
depends=('glibc')
options=(!makeflags !strip)
source=(http://www.oberhumer.com/opensource/lzo/download/lzo-${pkgver}.tar.gz)
md5sums=('95380bd4081f85ef08c5209f4107e9f8')

build() {
  cd "${srcdir}/lzo-${pkgver}"
  source $_cross_env

  ./configure \
        --build=$CHOST \
        --host=$_target \
        --prefix=/usr \
        --enable-shared
  make

  # build minilzo
  $CC $CFLAGS -fpic -Iinclude/lzo -o minilzo/minilzo.o -c minilzo/minilzo.c
  $CC $LDFLAGS -shared -o libminilzo.so.0 -Wl,-soname,libminilzo.so.0 minilzo/minilzo.o
}

check() {
  cd "${srcdir}/lzo-${pkgver}"
  #Can't easily test cross compiled binaries
  #make test # Larger test
  #make check
}

package() {
  cd "${srcdir}/lzo-${pkgver}"
  source $_cross_env

  make DESTDIR=${pkgdir} install

  # install minilzo
  install -m 755 libminilzo.so.0 ${pkgdir}/usr/lib
  install -p -m 644 minilzo/minilzo.h ${pkgdir}/usr/include/lzo
  cd ${pkgdir}/usr/lib
  ln -s libminilzo.so.0 libminilzo.so

  $STRIP $pkgdir/usr/lib/*.a
  $STRIP $pkgdir/usr/lib/*.so*
}
