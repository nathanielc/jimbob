# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc
# NOTE: valgrind requires rebuilt with each major glibc version

# NOTE: adjust version in install script when locale files are updated

pkgname=glibc
pkgver=2.19
pkgrel=5
pkgdesc="GNU C Library"
arch=('arm')
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
groups=('base')
makedepends=('kernel-headers>=3.13' 'gcc-newlib-ct')
backup=(etc/gai.conf
        etc/locale.gen
        etc/nscd.conf)
options=('!strip' 'staticlibs')
install=glibc.install
source=(http://ftp.gnu.org/gnu/libc/${pkgname}-${pkgver}.tar.xz{,.sig}
        glibc-2.19-xattr_header.patch
        glibc-2.19-fix-sign-in-bsloww1-input.patch
        glibc-2.19-tzselect-default.patch
        locale.gen.txt
        locale-gen)


md5sums=('e26b8cc666b162f999404b03970f14e4'
         'SKIP'
         '39a4876837789e07746f1d84cd8cb46a'
         '755a1a9d7844a5e338eddaa9a5d974cd'
         'c772dc99ddd8032ecbf43884ae0cf42e'
         '07ac979b6ab5eeb778d55f041529d623'
         '476e9113489f93b348b21e144b6a8fcf')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix for {linux,sys}/xattr.h incompatibility - commit fdbe8eae
  patch -p1 -i $srcdir/glibc-2.19-xattr_header.patch

  # fix issues in sin/cos slow path calculation - commit ffe768a9
  patch -p1 -i $srcdir/glibc-2.19-fix-sign-in-bsloww1-input.patch

  # fix tzselect with missing TZDIR - commit 893b4f37/c72399fb
  patch -p1 -i $srcdir/glibc-2.19-tzselect-default.patch

  rm -rf ${srcdir}/glibc-build
  mkdir ${srcdir}/glibc-build
}

build() {
  cd ${srcdir}/glibc-build
  #source $_cross_env


  echo "slibdir=/usr/lib" >> configparms
  echo "sbindir=/usr/bin" >> configparms
  echo "rootsbindir=/usr/bin" >> configparms

  # remove hardening options for building libraries
  CFLAGS=${CFLAGS/-fstack-protector-strong/}
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

  PATH=$_ct_gcc_path:$PATH

  ${srcdir}/${pkgname}-${pkgver}/configure \
        --prefix=/usr \
        --build=$CHOST \
        --host=$_target \
        --with-headers=$_ct_sysroot/usr/include \
        --enable-add-ons \
        --enable-kernel=2.6.32 \
        --disable-profile \
        libc_cv_forced_unwind=yes \
        libc_cv_ctors_header=yes \
        libc_cv_c_cleanup=yes
  make

}

check() {
  # the linker commands need to be reordered - fixed in 2.19
  LDFLAGS=${LDFLAGS/--as-needed,/}

  cd ${srcdir}/glibc-build

  # ULP failures on i686 are all small and can be ignored
  # tst-cleanupx4.out failure on i686 needs investigating...
  #make -k check || true
}

package() {
  cd ${srcdir}/glibc-build
  source $_cross_env

  install -dm755 ${pkgdir}/etc
  touch ${pkgdir}/etc/ld.so.conf

  make install_root=${pkgdir} -j1 install

  rm -f ${pkgdir}/etc/ld.so.{cache,conf}

  # Remove static libs
  #rm $pkgdir/usr/lib/*.a

  #install -dm755 ${pkgdir}/usr/lib/{locale,systemd/system,tmpfiles.d}

  #install -m644 ${srcdir}/${pkgname}-${pkgver}/nscd/nscd.conf ${pkgdir}/etc/nscd.conf
  ##install -m644 ${srcdir}/${pkgname}-${pkgver}/nscd/nscd.service ${pkgdir}/usr/lib/systemd/system
  #install -m644 ${srcdir}/${pkgname}-${pkgver}/nscd/nscd.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/nscd.conf

  #install -m644 ${srcdir}/${pkgname}-${pkgver}/posix/gai.conf ${pkgdir}/etc/gai.conf

  #install -m755 ${srcdir}/locale-gen ${pkgdir}/usr/bin

  ## create /etc/locale.gen
  #install -m644 ${srcdir}/locale.gen.txt ${pkgdir}/etc/locale.gen
  #sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
  #  ${srcdir}/glibc-${pkgver}/localedata/SUPPORTED >> ${pkgdir}/etc/locale.gen


  # Do not strip the following files for improved debugging support
  # ("improved" as in not breaking gdb and valgrind...):
  #   ld-${pkgver}.so
  #   libc-${pkgver}.so
  #   libpthread-${pkgver}.so
  #   libthread_db-1.0.so

  source $_cross_env
  cd $pkgdir
  $STRIP $STRIP_BINARIES usr/bin/{gencat,getconf,getent,iconv,iconvconfig} \
                        usr/bin/{ldconfig,locale,localedef,nscd,makedb} \
                        usr/bin/{pcprofiledump,pldd,rpcgen,sln,sprof} \

  $STRIP $STRIP_STATIC usr/lib/*.a

  $STRIP $STRIP_SHARED usr/lib/{libanl,libBrokenLocale,libcidn,libcrypt}-*.so \
                      usr/lib/libnss_{compat,db,dns,files,hesiod,nis,nisplus}-*.so \
                      usr/lib/{libdl,libm,libnsl,libresolv,librt,libutil}-*.so \
                      usr/lib/{libmemusage,libpcprofile,libSegFault}.so \
                      usr/lib/{audit,gconv}/*.so
}
